#ifndef ORO_CORBA_DATAFLOW_IDL
#define ORO_CORBA_DATAFLOW_IDL

#include "Operations.idl"

module RTT
{
  module Corba
  {

    /**
     * A buffer consists of an event channel and
     * some query methods for reading the buffer's state.
     * @ingroup CompIDL
     */
    interface BufferChannel 
    {

      boolean push(in any value);

      boolean pull(out any value);

      any front();

      boolean full();

      boolean empty();

      long capacity();

      long size();

      void clear();
    };

    /**
     * An interface to access the dataflow
     * of a ControlTask object. Data ports are exported as
     * assignable expressions (Set/Get). Buffer ports are
     * exported as buffer channels.
     * @ingroup CompIDL
     */
    interface DataFlowInterface
    {

      typedef sequence<string> PortNames;

      enum PortType { ReadPort, WritePort, ReadWritePort };
      enum ConnectionModel { Buffered, Data };

      /**
       * Returns the names of the ports of this component.
       */
      PortNames getPorts();

      /**
       * Creates a data connection for an Orocos data port. 
       * You may use the returned AssignableExpression
       * to write data to it or read data from it.
       *
       */
      AssignableExpression createDataChannel(in string port_name);

      /**
       * Creates an bufferd channel for an Orocos port which will be used
       * in a 'push' or 'pull' model. You may use the returned BufferChannel
       * to write data to it or read data from it.
       *
       */
      BufferChannel createBufferChannel(in string port_name);

      /**
       * Creates a read-only data object which represents the status
       * of a Data or Buffer Channel connection. For a DataChannel, it returns the
       * current value, for a BufferChannel, it returns the value of
       * the front() element.
       * If the port is not connected, nill is returned.
       */
      Expression createDataObject(in string port_name);

      /**
       * Check the kind of channel one can get from a given port
       */
      ConnectionModel getConnectionModel(in string port_name);

      /**
       * Check if a port will read from or write to a Channel.
       */
      PortType getPortType(in string port_name);

      /**
       * Get the type of data this port processes.
       */
      string getDataType(in string port_name);

      /**
       * Check if a port is already connected. If so, one can join
       * in by using the create methods above. If not, the create
       * methods above will setup a new connection.
       */
      boolean isConnected(in string port_name);
      
      /**
       * Connect a data port from this interface with an external
       * data channel.
       */
      boolean connectDataPort(in string port_name, in AssignableExpression data);

      /**
       * Connect a buffer port from this interface with an external
       * buffer channel.
       */
      boolean connectBufferPort(in string port_name, in BufferChannel buffer); 

      /**
       * Connect the given named port to the given remote port
       */
      boolean connectPorts(in string local_port, in DataFlowInterface remote_ports, in string remote_port, in boolean is_buffer);
    };
  };
};
#endif
